plugins {
    id 'java-conventions'
    id 'jacoco-report-aggregation'
    id 'application'
    id 'org.springframework.boot' version '3.1.3'
    id 'io.spring.dependency-management' version '1.1.3'
}

group = 'com.study.expensetracker.infrastructure'


dependencies {
    implementation(project(":domain"))
    implementation(project(":application"))

    implementation ('org.flywaydb:flyway-core')
    runtimeOnly ('org.postgresql:postgresql')

    implementation('org.yaml:snakeyaml:2.0'){
        version {strictly("2.0")}
    }

    implementation('org.springframework.boot:spring-boot-starter-web') {
        exclude module: 'spring-boot-starter-tomcat'
    }
    implementation('org.springframework.boot:spring-boot-starter-undertow')
    implementation('org.springframework.boot:spring-boot-starter-data-jpa')
    implementation('org.springframework.boot:spring-boot-starter-amqp')

    implementation('org.springdoc:springdoc-openapi-starter-webmvc-ui:2.0.3')

    implementation('com.fasterxml.jackson.module:jackson-module-afterburner')

    testImplementation('org.springframework.boot:spring-boot-starter-test')
}

testCodeCoverageReport {
    reports {
        xml.outputLocation = file("$rootDir/build/reports/jacoco/test/jacocoTestReport.xml")
        html.outputLocation = file("$rootDir/build/reports/jacoco/test/")
    }
}

jacocoTestReport {
    dependsOn tasks.named('testCodeCoverageReport', JacocoReport)
}

springBoot {
    mainClass = "com.study.expensetracker.infrastructure.Main"
}

bootJar {
    archivesBaseName = 'application'
    destinationDirectory.set(file("${rootProject.buildDir}/libs"))
}